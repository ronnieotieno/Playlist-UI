plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "dev.challenge.soundcloudapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "CLIENT_ID", "\"" + getClientId() + "\"")
        buildConfigField("String", "CLIENT_SECTRET", "\"" + getClientSecret() + "\"")
        buildConfigField("String", "BASE_URL", "\"" + getBaseUrl() + "\"")
    }

    buildFeatures {
        dataBinding = true

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def recyclerview = "1.2.1"
    def retrofit = "2.9.0"
    def logging = "4.7.2"
    def dagger = "2.33"
    def support = "2.27"
    def glide = "4.11.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //recyclerview
    implementation("androidx.recyclerview:recyclerview:$recyclerview") {
        because("Recyclerview is needed to show the data in list")
    }

    //Retrofit and Gson
    implementation("com.squareup.retrofit2:retrofit:$retrofit") {
        because("The retrofit is the networking library")
    }
    implementation("com.squareup.retrofit2:converter-gson:$retrofit") {
        because("Used to serialize the json from the api to data classes")
    }
    implementation("com.squareup.okhttp3:logging-interceptor:$logging") {
        because("Used to debug the requests in debug mode")
    }

    implementation('androidx.fragment:fragment-ktx:1.3.6') {
        ("Provide lifecycle scope for courotine as well as livedata")
    }

    implementation('androidx.legacy:legacy-support-v4:1.0.0') {
        because("Dagger2 needs legacy code, the project is in androidX but Dagger2 uses support libraries")
    }

    //Dagger2
    implementation("com.google.dagger:dagger:$dagger") {
        because("Dagger2 for dependency injection")
    }
    implementation "com.google.dagger:dagger-android-support:$support"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$support"

    //glide
    implementation("com.github.bumptech.glide:glide:$glide") {
        because("To load the image from url")
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide"

}

def getClientId() {
    return project.findProperty("client_id")
}

def getClientSecret() {
    return project.findProperty("client_secret")
}
def getBaseUrl() {
    return project.findProperty("base_url")
}